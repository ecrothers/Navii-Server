buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:1.2.6.RELEASE")
    }
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'spring-boot'

mainClassName = 'com.navii.server.Application'

version = '0.1-SNAPSHOT'

repositories {
    mavenCentral()
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

sourceSets {
    integrationTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/integration-test/java')
        }
        resources {
            srcDir file('src/integration-test/resources')
        }
    }
}

configurations {
    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime
}

dependencies {
    def springBootVersion = '1.3.0.RELEASE'

    compile("org.springframework.boot:spring-boot-starter-thymeleaf:$springBootVersion")
    compile("org.springframework.boot:spring-boot-starter-web:$springBootVersion")
    compile("org.springframework.boot:spring-boot-starter-jdbc:$springBootVersion")
    compile("mysql:mysql-connector-java:5.1.34")
    compile("ch.qos.logback:logback-classic:1.0.13")
    compile("com.amazonaws:aws-java-sdk-s3:1.10.26")
    compile("com.google.guava:guava:18.0")
    compile("com.fasterxml.jackson.module:jackson-module-afterburner:2.6.3")
    compile("org.springframework.retry:spring-retry:1.1.2.RELEASE")

    testCompile("org.springframework.boot:spring-boot-starter-test:$springBootVersion")
    testCompile("org.springframework:spring-test:4.2.2.RELEASE")
    testCompile("junit:junit:4.12")
}

jar {
    baseName = 'gs-navii'
    version =  '0.0.1'
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.7'
}

task integrationTest(type: Test) {
    testClassesDir = sourceSets.integrationTest.output.classesDir
    classpath = sourceSets.integrationTest.runtimeClasspath
    outputs.upToDateWhen { false }
}

check {
    dependsOn integrationTest
}